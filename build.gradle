plugins {
    id "java-library"
    id "maven-publish"
    id "com.timgroup.jarmangit" version "1.1.86"
}

group = 'com.timgroup'
if (System.getenv("BUILD_NUMBER")) version = "1.0.${System.getenv('BUILD_NUMBER')}"

ext {
    slf4jVersion = "1.7.25"
}

jar {
    manifest { 
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.withType(JavaCompile).all {
    options.encoding = "UTF-8"
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

tasks.named("jar").configure {
    manifest {
        attributes(
                'Automatic-Module-Name': 'com.timgroup.tucker'
        )
    }
}

repositories {
    jcenter()
}

// some of these dependencies are optional, in that they are needed to build, but might not be needed to run; Gradle does currently let us say so
dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "javax.servlet:javax.servlet-api:3.0.1"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.0"
    implementation "io.prometheus:simpleclient_dropwizard:0.6.0"
    implementation "io.prometheus:simpleclient_common:0.6.0"
    api "io.dropwizard.metrics:metrics-core:3.2.0"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.25.1"
    testImplementation "org.araqnid:hamkrest-json:1.1.0"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.6.0"
    testImplementation "com.google.guava:guava:28.0-jre"

    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                name "nexus"
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId project.ext.has('artifactId') ? project.ext.artifactId
                    : project == rootProject ? project.name
                    : rootProject.name + project.path.replace(':', '-')
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.ext.has('pomName') ? project.ext.pomName : project.name
                    description project.description
                    url "https://github.com/tim-group/Tucker"
                    licenses {
                        license {
                            name 'The BSD 2-Clause License'
                            url 'http://opensource.org/licenses/BSD-2-Clause'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            email 'opensource@timgroup.com'
                        }
                    }
                }
            }
        }
    }
}
