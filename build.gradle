apply plugin: 'java'
apply plugin: 'maven'
def doSigning = gradle.startParameter.taskNames.contains("signArchives")
if (doSigning) {
    apply plugin: 'signing'
}
apply plugin: 'eclipse'

group = 'com.timgroup'
ext.url = 'https://github.com/youdevise/Tucker'

sourceCompatibility = 1.8

def majorVersion = 1
def minorVersion = 0

/** for Maven Central; manually bump major and/or minor version numbers before building */
def isPublicRelease() {
    return "".equals(System.getenv('BUILD_NUMBER'))
}

/** CI builds */
def isPrivateRelease() {
    return !isPublicRelease() && System.getenv('BUILD_NUMBER') != null
}

if (isPublicRelease()) {
    version = "${majorVersion}.${minorVersion}"
} else if (isPrivateRelease()) {
    def buildNumber = System.getenv('BUILD_NUMBER')
    version = "${majorVersion}.${minorVersion}.${buildNumber}"
} else /* dev build */ {
    def timestamp = new Date().format('yyyyMMdd-HHmmss', TimeZone.getTimeZone('UTC'))
    version = "${majorVersion}.${minorVersion}-${timestamp}"
}

jar {
    manifest { 
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'youDevise Ltd',
            'Implementation-Vendor-Id': project.group,
            'Implementation-URL': project.url
        )
    }
}

repositories {
    mavenLocal()
    if (project.hasProperty('repoUrl')) {
        mavenRepo url: "${project.repoUrl}/groups/public"
    }
    else {
        mavenCentral()
    }
}

def slf4jVersion = '1.7.5'

// some of these dependencies are optional, in that they are needed to build, but might not be needed to run; Gradle does currently let us say so
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1' // optional
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.0'
    
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.0'
    
    testRuntime group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
 
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task pom {
    ext.pom = pom {
        project {
            name project.name
            description 'Tucker is a small library for gently and politely helping with the communication and management of application status.'
            url project.url
            
            packaging 'jar'
            
            scm {
                url "scm:${project.url}"
                connection 'scm:git:https://github.com/youdevise/Tucker.git'
                developerConnection 'scm:git:git://github.com/youdevise/Tucker.git'
            }
            
            licenses {
                license {
                    name 'The BSD 2-Clause License'
                    url 'http://opensource.org/licenses/BSD-2-Clause'
                    distribution 'repo'
                }
            }
            
            developers {
                developer {
                    email 'opensource@timgroup.com'
                }
            }
        }
    }
} << {
    pom.writeTo("$buildDir/pom.xml")
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (doSigning) {
                beforeDeployment { deployment ->
                    signing.signPom(deployment)
                }
            }
            
            if (project.hasProperty('repoUrl')) {
                repository(url: "${project.repoUrl}/repositories/yd-release-candidates") {
                    authentication(userName: project.repoUsername, password: project.repoPassword)
                }
            }
            
            pom = tasks.pom.pom
        }
    }
}
uploadArchives.dependsOn pom

if (doSigning) {
    signing {
        sign configurations.archives
    }
    
    signArchives.doFirst {
        assert file('gradle.properties').isFile(), "in order to sign anything, you need to supply signing properties via gradle.properties - see http://www.gradle.org/docs/current/userguide/signing_plugin.html"
    }
}

eclipse {
    classpath {
        defaultOutputDir = file('.eclipse')
    }
}

task hgignore << {
    // gitignore and hgignore syntax are similar enough that this works (for now)
    file('.hgignore').withWriter { out ->
        out.println 'syntax: glob'
        out.println '.hgignore'
        file('.gitignore').eachLine { line ->
            out.println line
        }
    }
}
