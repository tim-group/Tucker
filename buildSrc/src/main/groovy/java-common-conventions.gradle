plugins {
    id "java-library"
}

group = 'com.timgroup'

def buildNumber = providers.environmentVariable("BUILD_NUMBER")
if (buildNumber.present) version = "1.0.${buildNumber.get()}"

tasks.withType(Jar).configureEach {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group'
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

extensions.create("timgroupRepo", com.timgroup.gradle.TimRepoExtension)

repositories {
    mavenCentral()
    if (timgroupRepo.nexusRepoUrl.present) {
        maven {
            name = "nexus"
            url = "${timgroupRepo.nexusRepoUrl.get()}/repositories/yd-release-candidates"
            allowInsecureProtocol = true
        }
    }
    if (timgroupRepo.codeartifactUrl.present && timgroupRepo.codeartifactToken.present) {
        maven {
            name = "codeartifact"
            url = timgroupRepo.codeartifactUrl.get()
            credentials {
                username = "aws"
                password = timgroupRepo.codeartifactToken.get()
            }
        }
    }
}
