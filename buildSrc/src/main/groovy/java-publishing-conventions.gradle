plugins {
    id "maven-publish"
}

publishing {
    repositories {
        if (timgroupRepo.nexusRepoUrl.present && timgroupRepo.nexusRepoUsername.present && timgroupRepo.nexusRepoPassword.present) {
            maven {
                name = "nexus"
                url = "${timgroupRepo.nexusRepoUrl.get()}/repositories/yd-release-candidates"
                credentials {
                    username = timgroupRepo.nexusRepoUsername.get()
                    password = timgroupRepo.nexusRepoPassword.get()
                }
                allowInsecureProtocol = true
            }
        }
        if (timgroupRepo.codeartifactUrl.present && timgroupRepo.codeartifactToken.present) {
            maven {
                name = "codeartifact"
                url = timgroupRepo.codeartifactUrl.get()
                credentials {
                    username = "aws"
                    password = timgroupRepo.codeartifactToken.get()
                }
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId project.ext.has('artifactId') ? project.ext.artifactId
                        : project == rootProject ? project.name
                        : rootProject.name + "-" + project.name
                from components.java
            }
        }
    }
}