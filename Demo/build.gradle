plugins {
    id "java"
    id "application"
    id "maven-publish"
}

repositories {
    mavenLocal()
    jcenter()
}

ext {
    slf4jVersion = '1.7.13'
    jettyVersion = '9.4.14.v20181114'
    httpPort = 8888
}

tasks.withType(JavaCompile).all {
    options.encoding = "UTF-8"
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.named("jar").configure {
    manifest {
        attributes(
                'Automatic-Module-Name': 'com.timgroup.tucker.demo'
        )
    }
}

dependencies {
    implementation project(":")
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"

    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    implementation "commons-io:commons-io:1.4"
}

mainClassName = 'infrastructure.JettyLauncher'

run {
    args = [httpPort.toString()]
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "Tucker-Demo"
            from components.java
        }
    }
}
